# vim: set et ai ts=4 sw=4:
# -------------------------------------------------------------------------
# Copyright (c) 2011, Peter Sommerlad and IFS Institute for Software
# at HSR Rapperswil, Switzerland
# All rights reserved.
#
# This library/application is free software; you can redistribute and/or
# modify it under the terms of the license that is included with this
# library/application in the file license.txt.
# -------------------------------------------------------------------------

import pkg_resources
pkg_resources.require(["SConsider"])
from SomeUtils import listFiles
Import('*')

_sconsider_dist = pkg_resources.get_distribution("SConsider").parsed_version
if _sconsider_dist < pkg_resources.parse_version("0.5"):
    from SConsider import createTargets
    from SConsider.PackageRegistry import PackageRegistry
    buildSettings = {
        'include': {
            'public': {
                'includeSubdir': '',
            },
        },
        'regex': {
            'targetType': 'LibraryShared',
            'targetName': packagename + '_regex',
            'linkDependencies': [
                # used to pass public include dirs of boost
                PackageRegistry.createFulltargetname(packagename, 'include'),
            ],
            'sourceFiles': listFiles(['libs/regex/src/*.cpp']),
            # 'appendUnique'     : {
            #     'CPPDEFINES'   : ['BOOST_HAS_ICU=1'],
            #     'LIBS'         : ['icui18n','icuuc'],
            # },
        },
        'system': {
            'targetType': 'LibraryShared',
            'targetName': packagename + '_system',
            'linkDependencies': [
                # used to pass public include dirs of boost
                PackageRegistry.createFulltargetname(packagename, 'include'),
            ],
            'sourceFiles': listFiles(['libs/system/src/*.cpp']),
        },
    }

    createTargets(packagename, buildSettings)
elif _sconsider_dist >= pkg_resources.parse_version("0.5"):
    from TargetMaker import EnvBuilder
    from TargetHelpers import createPackageSharedLibrary
    includeTarget = EnvBuilder(export=[EnvBuilder().include(Dir('.'))]).build().RegisterPackageTarget(
        packagename=packagename, targetname='include')

    all_libs = []
    for libname in ['regex', 'system']:
        shared_lib = createPackageSharedLibrary(packagename,
                                                'boost_' + libname,
                                                listFiles(['libs/' + libname + '/src/*.cpp']),
                                                depend=[includeTarget])
        # register lib with backward compatibility name
        EnvBuilder().depend(shared_lib).build().RegisterPackageTarget(packagename=packagename,
                                                                      targetname=libname)
        all_libs.append(shared_lib)

    libs = EnvBuilder().depend(all_libs).build().RegisterPackageTarget(packagename=packagename,
                                                                       targetname='libs')

    EnvBuilder().depend(includeTarget, libs).build().RegisterPackageTarget(packagename=packagename,
                                                                           targetname=packagename)
